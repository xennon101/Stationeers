alias activate d0
alias typeTemp d1
alias quantityPressure d2
alias typeDial d3
alias quantityDial d4
alias furnace d5
alias type r0
alias quantity r1
alias isActive r2
alias reagents r3
alias expectedReagents r4
alias maxTemp r5
alias maxPressure r6
alias minTemp r7
alias minPressure r8
alias currentTemp r9
alias currentPressure r10
alias status r11
reset:
s db Setting 0
move maxTemp 1890 #default values
move maxPressure 3490
move minTemp 710
move minPressure 810
s typeTemp Setting 0
s quantityPressure Setting 0
s furnace Open 1
s furnace Lock 1
waitActive:
yield
l isActive activate Setting
l type typeDial Setting
l quantity quantityDial Setting
beqz isActive waitActive #Activate
beqz type waitActive #reset if invalid
beqz quantity waitActive
s furnace Open 0 #Start Ore Selection
s typeTemp Setting type
s db Setting 1
mul expectedReagents 100 quantity #For 100 alloys
beq type 8 constantan #choose alloy or normal ores
beq type 9 electrum
beq type 10 invar
beq type 11 waitOre
mul expectedReagents 200 quantity #For 200 alloys
beq type 12 waitOre
beq type 13 astroloy
beq type 14 hastelloy
beq type 15 inconel
beq type 16 stellite
beq type 17 waspaloy
mul expectedReagents 50 quantity #Regulare Ore
j waitOre
constantan: #All alloy push combo of ores to stack
move maxTemp 1490
move minTemp 1010
j waitOre
electrum: #All alloy push combo of ores to stack
move maxPressure 2390
j waitOre
invar:
move maxPressure 6990
move minPressure 6010
move minTemp 1210
j waitOre
astroloy:
move maxPressure 5990
move minPressure 5010
move maxTemp 1390
move minTemp 1210
j waitOre
hastelloy:
move maxPressure 3000
move minPressure 2500
move maxTemp 1000
move minTemp 950
j waitOre
inconel:
move maxPressure 4730
move minPressure 4270
move maxTemp 1285
move minTemp 1215
j waitOre
stellite:
move maxPressure 4990
move minPressure 4010
move maxTemp 1890
move minTemp 1710
j waitOre
waspaloy:
move maxPressure 2740
move minPressure 1260
move maxTemp 990
move minTemp 880
waitOre:
yield
s db Setting 2
l type typeTemp Setting
beq type -1 fail #Mem=-1 for failure
bne type 50 waitOre #Mem=50 ores complete
s furnace Lock 0 #Unlock the furnace/chute
s typeTemp Setting maxTemp #Temp and Press to mem
s quantityPressure Setting maxPressure
waitSmelt: #Wait while the reagents smelt
yield
l currentTemp furnace Temperature
l currentPressure furnace Pressure
blt currentTemp minTemp waitSmelt
blt currentPressure minPressure waitSmelt
bgt currentTemp maxTemp waitSmelt
bgt currentPressure maxPressure waitSmelt
l reagents furnace Reagents
beq reagents expectedReagents empty #Check Success
j waitSmelt
fail:
s db setting -1
yield
l currentTemp furnace Temperature
bgt currentTemp 300 fail #Low temp for failure
empty:
s furnace Lock 0
s furnace Open 1 #Open up the furnace
l status db Setting
brge status 0 2
sleep 10 #Sleep in case of fail
yield
l reagents furnace Reagents #Check empty furnace
beqz reagents waitActive
j empty